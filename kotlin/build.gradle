group 'codedgame'
version '0.0.0'

buildscript {
    ext.kotlin_version = '1.3.0'

    repositories {
        jcenter()
        mavenCentral()
    }

    dependencies {
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlin_version"
    }
}

wrapper {
    gradleVersion = '5.0'
}

subprojects {
    buildscript {
        repositories {
            jcenter()
            mavenCentral()
        }
    }

    repositories {
        jcenter()
        mavenCentral()
    }

    apply plugin: 'kotlin'

    test {
        useJUnitPlatform()
    }

    sourceSets {
        main.kotlin.srcDirs = ['src']
        test.kotlin.srcDirs = ['test']
    }

    compileKotlin.kotlinOptions.jvmTarget = "1.8"
    compileTestKotlin.kotlinOptions.jvmTarget = "1.8"

    configurations.all {
        resolutionStrategy.eachDependency { details ->
            if (details.requested.group == 'org.jetbrains.kotlin') {
                details.useVersion kotlin_version
                details.because 'force kotlin version'
            }
        }
    }

    dependencies {
        compile "org.jetbrains.kotlin:kotlin-stdlib"

        testCompile 'org.junit.jupiter:junit-jupiter-api:5.3.1'
        testCompile 'io.kotlintest:kotlintest-runner-junit5:3.1.10'
    }
}

configure(subprojects.findAll { it.name != "utils" }) {
    dependencies {
        compile project(":utils")
    }
}